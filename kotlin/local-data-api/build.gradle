buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
              url "https://plugins.gradle.org/m2/"
            }
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'kotlinx-serialization'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "jacoco"

group 'com.koxudaxi.local-data-api'
version '0.6.0'
mainClassName = "io.ktor.server.netty.EngineMain"


sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

test {
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7-SNAPSHOT"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${projectDir}/jacocoHtml")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    excludes: [
                            // ignore generated data class models.
                            '**/localDataApi/*Request*',
                            '**/localDataApi/*Response*',
                            '**/localDataApi/ColumnMetadata.class',
                            '**/localDataApi/UpdateResult.class',
                            '**/localDataApi/SqlParameter.class',
                            '**/localDataApi/DataAPIException.class',
                            '**/localDataApi/ApplicationKt.class',
                    ]
            )
        }))
    }
    dependsOn test
}
shadowJar{
    mergeServiceFiles()
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"
    implementation "io.ktor:ktor-serialization:$ktor_version"
    implementation "org.mariadb.jdbc:mariadb-java-client:2.7.2"
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    implementation "org.httprpc:httprpc-client:8.1.2"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "com.h2database:h2:1.4.200"
    testImplementation "io.mockk:mockk:1.10.6"
}
